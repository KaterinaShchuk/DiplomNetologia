
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

//Начало Доработка Щукина Е.Д.
//Реализация для документа печатной формы акта об оказанных услугах

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОбОказанныхУслугах";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказанных услугах'");
	КомандаПечати.Порядок = 5;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктОбОказанныхУслугах") Тогда
	    УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктОбОказанныхУслугах", "Акт об оказанных услугах", ПечатьАкта(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), , "Документ.РеализацияТоваровУслуг.ПФ_MXL_ВКМ_АктОбОказанныхУслугах");
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьАкта(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АктОбОказанныхУслугах";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_ВКМ_АктОбОказанныхУслугах");
	
	ДанныеДокумента = ПолучитьДанныеДокумента(МассивОбъектов);
	
	Пока ДанныеДокумента.Следующий() Цикл
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		ОбластьЗаголовок.Параметры.НомерДокумента = ДанныеДокумента.Номер;
		ОбластьЗаголовок.Параметры.ДатаДокумента = Формат(ДанныеДокумента.Дата, "ДФ=dd.MM.yyyy;");
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьШапка.Параметры.Организация = ДанныеДокумента.Организация;
		ОбластьШапка.Параметры.Контрагент = ДанныеДокумента.Контрагент;
		ОбластьШапка.Параметры.Договор = ДанныеДокумента.Договор;
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		ОбластьТоварыШапка = Макет.ПолучитьОбласть("ТоварыШапка");
		ТабличныйДокумент.Вывести(ОбластьТоварыШапка);
		
		ОбластьТовары = Макет.ПолучитьОбласть("Товары");
		ВыборкаТовары = ДанныеДокумента.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			ОбластьТовары.Параметры.Заполнить(ВыборкаТовары);
			ТабличныйДокумент.Вывести(ОбластьТовары, ВыборкаТовары.Уровень());
		КонецЦикла;		
		
		ОбластьУслугиШапка = Макет.ПолучитьОбласть("УслугиШапка");
		ТабличныйДокумент.Вывести(ОбластьУслугиШапка);
		
		ОбластьУслуги = Макет.ПолучитьОбласть("Услуги");
		ВыборкаУслуги = ДанныеДокумента.Услуги.Выбрать();
		Пока ВыборкаУслуги.Следующий() Цикл
			ОбластьУслуги.Параметры.Заполнить(ВыборкаУслуги);
			ТабличныйДокумент.Вывести(ОбластьУслуги, ВыборкаУслуги.Уровень());
		КонецЦикла;

		ОбластьИтого = Макет.ПолучитьОбласть("Итого");
		ОбластьИтого.Параметры.СуммаДокумента = ДанныеДокумента.СуммаДокумента;
		СуммаПрописью = ЧислоПрописью(ДанныеДокумента.СуммаДокумента, "Л = ru_RU; ДП = Истина","рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2");
		ОбластьИтого.Параметры.СуммаДокументаПрописью = СуммаПрописью;
		ТабличныйДокумент.Вывести(ОбластьИтого);
		
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		ОбластьПодвал.Параметры.Ответственный = ДанныеДокумента.Ответственный;
		ОбластьПодвал.Параметры.Контрагент = ДанныеДокумента.Контрагент;
		ТабличныйДокумент.Вывести(ОбластьПодвал);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;

КонецФункции

Функция ПолучитьДанныеДокумента(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Номер,
		|	РеализацияТоваровУслуг.Дата,
		|	РеализацияТоваровУслуг.Организация,
		|	РеализацияТоваровУслуг.Контрагент,
		|	РеализацияТоваровУслуг.Договор,
		|	РеализацияТоваровУслуг.СуммаДокумента,
		|	РеализацияТоваровУслуг.Ответственный,
		|	РеализацияТоваровУслуг.Товары.(
		|		Номенклатура,
		|		Количество,
		|		Цена,
		|		Сумма),
		|	РеализацияТоваровУслуг.Услуги.(
		|		Номенклатура,
		|		Количество,
		|		Цена,
		|		Сумма)
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.Ссылка В (&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

//Конец Доработка Щукина Е.Д.
//Реализация для документа печатной формы акта об оказанных услугах

#КонецОбласти

#КонецЕсли

