
&НаКлиенте
Процедура ВКМ_ЗапуститьОперацию(Команда)
	
    ИдЗадания  = "";
	ПараметрыЗапуска = ПодготовитьДанныеДляДлительнойОперации(); 
	СтруктураФоновогоЗадания = ВыполнитьФоновоеЗаданиеНаСервере(ПараметрыЗапуска, УникальныйИдентификатор);
	ИдЗадания =  СтруктураФоновогоЗадания.ИдентификаторЗадания;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.Интервал = 2;

    //Ожидать завершения выполнение процедуры в фоновом задании и открыть форму ожидания длительной операции.
	ДлительныеОперацииКлиент.ОжидатьЗавершение(СтруктураФоновогоЗадания, 
	Новый ОписаниеОповещения("ОбработатьДанные", ЭтотОбъект),
	ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Функция ПодготовитьДанныеДляДлительнойОперации()
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("НачалоПериода", НачалоМесяца(Объект.ВКМ_ПериодДляУказанияМесяца));
	СтруктураДанных.Вставить("КонецПериода", КонецМесяца(Объект.ВКМ_ПериодДляУказанияМесяца));
	
	Возврат СтруктураДанных;	
КонецФункции

&НаСервереБезКонтекста
Функция ВыполнитьФоновоеЗаданиеНаСервере(ПараметрыЗапуска, УникальныйИдентификатор)

	НаименованиеЗадания = НСтр("ru = 'Массовое создание актов'");
	
	ВыполняемыйМетод = "ВКМ_ПолучениеДанныхАктов.МассовоеСозданиеАктов";
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.Вставить("ИдентификаторФормы", УникальныйИдентификатор); 
	
	СтруктураФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыЗапуска, ПараметрыВыполнения);
	
	Возврат СтруктураФоновогоЗадания;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьДанные(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		// обрабатываем результат
		Данные = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		ВозвратноеЗначение = Данные.Количество();
	КонецЕсли;

КонецПроцедуры
